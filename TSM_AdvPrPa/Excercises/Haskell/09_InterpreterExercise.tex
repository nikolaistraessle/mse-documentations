\section{Interpreter Exercise}

\begin{lstlisting}[language=Haskell]
divProg =
  CpsCmd [
    AssiCmd "q" (LitAExpr 0),
    AssiCmd "r" (IdAExpr "m"),
    WhileCmd (RelBExpr GreaterEq (IdAExpr "r") (IdAExpr "n"))
      (CpsCmd [
         AssiCmd "q" (DyaAExpr Plus (IdAExpr "q") (LitAExpr 1)),
         AssiCmd "r" (DyaAExpr Minus (IdAExpr "r") (IdAExpr "n"))
      ])
  ]

type Value = Int
type Ident = String

type State = Ident -> Value

state_q5 "q" = 5
state_q5 _ = 0

readS :: State -> Ident -> Value
readS state ident = state ident

updateS :: State -> (Ident, Value) -> State
updateS s (ident, val) =
  \ident' -> if ident' == ident then val
             else s ident'

data ArithExpr
  = LitAExpr Int   -- Literal Arithmetic Expression
  | IdAExpr Ident
  | DyaAExpr ArithOperator ArithExpr ArithExpr   -- dyadic, means two
  deriving Show

data ArithOperator
  = Times | Div | Mod | Plus | Minus
  deriving Show

-- (2 + 3) * 4
ae1, ae2 :: ArithExpr
ae1 = DyaAExpr Plus (LitAExpr 2) (LitAExpr 3)
ae2 = DyaAExpr Times ae1 (LitAExpr 4)

-- (q + 1)
aeq1 = DyaAExpr Plus (IdAExpr "q") (LitAExpr 1)
-- (r - n)
aern = DyaAExpr Minus (IdAExpr "r") (IdAExpr "n")

evalAExpr :: ArithExpr -> State -> Int   -- Semantic function
evalAExpr (LitAExpr c) _ = c
evalAExpr (IdAExpr ident) state = readS state ident
evalAExpr (DyaAExpr opr expr1 expr2) state =
    operation val1 val2
  where operation = evalAOpr opr
        val1 = evalAExpr expr1 state
        val2 = evalAExpr expr2 state

evalAOpr :: ArithOperator -> (Int -> Int -> Int)
evalAOpr Times = (*)
evalAOpr Div = div
evalAOpr Mod = mod
evalAOpr Plus = \a b -> a + b
evalAOpr Minus = (-)

evalAOprV2 :: ArithOperator -> (Int -> Int -> Int)
evalAOprV2 Times a b = a * b



data RelOperator
  = LessEq
  | GreaterEq
  deriving Show

evalROpr :: RelOperator -> (Value -> Value -> Bool)
evalROpr LessEq = (<=)
evalROpr GreaterEq = (>=)

data BoolExpr
  = RelBExpr RelOperator ArithExpr ArithExpr
  deriving Show

evalBExpr :: BoolExpr -> State -> Bool   -- Semantic function
evalBExpr (RelBExpr opr expr1 expr2) state =
    operation val1 val2
  where operation = evalROpr opr
        val1 = evalAExpr expr1 state
        val2 = evalAExpr expr2 state

data Command
  = AssiCmd Ident ArithExpr
  | CpsCmd [Command]
  | WhileCmd BoolExpr Command
  deriving Show

interCmd :: Command -> State -> State
interCmd (AssiCmd ident expr) state =
    updateS state (ident, val)
  where
    val = evalAExpr expr state

interCmd (CpsCmd cmds) state =
  foldl (flip interCmd) state cmds

interCmd (WhileCmd guard repetend) state
  | evalBExpr guard state =
      interCmd (WhileCmd guard repetend) state'
  | otherwise = state
        where state' = interCmd repetend state

state175 "m" = 17
state175 "n" = 5
state175 _ = 0

stateOut = interCmd divProg state175

qres = readS stateOut "q"
rres = readS stateOut "r"

--evalAOpr :: ArithOperator -> (Int -> Int -> Int)
--evalAOpr Times = (*)

--data T = Prelude.False
\end{lstlisting}
\clearpage